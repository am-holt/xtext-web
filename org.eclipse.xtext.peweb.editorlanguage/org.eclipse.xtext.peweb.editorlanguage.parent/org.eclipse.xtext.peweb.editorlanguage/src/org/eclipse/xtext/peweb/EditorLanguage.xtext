grammar ox.xtext.projectionalEditor.EditorLanguage
hidden(WS, ML_COMMENT, SL_COMMENT)
     
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
     
generate editorLanguage "http://www.xtext.ox/projectionalEditor/EditorLanguage"

EditorDefinitionFile:
	(imports = ImportSection)?
	(mainSection = MainSection);
	
ImportSection:
	{ImportSection}
	imports+=Import+ 
;

Import:
	'import' name=QualifiedName
;

QualifiedName:
    ID ('.' ID)*;

MainSection:
	{MainSection}
	(nodes+=NodeDeclaration | components += ComponentDeclaration)*
;

NodeDeclaration:
	'Node' nodeName=ID '{'
	(projections+=ProjectionDeclaration | components+=ComponentDeclaration)*
	'}'
;

ProjectionDeclaration:
	'Projection' name=ID '{'
	'view:' view=HtmlSnippet
	(attributes+=AttributeDeclaration | components+=ComponentDeclaration)*
	'}'
;

AttributeDeclaration:
	'@' attributeName=ID controller=AttributeController
;

ComponentId:
	'%'componentType=[ComponentDeclaration] (':' componentId=ID)? '%' 
;

AttributeController:
	'{' 
		'get:' getter=JsSnippet
		'set:' setter=JsSnippet
		'validate:' validater=JsSnippet
	'}'
;

ChildId: 
	'!' childId=ID ':' projectionId=[ProjectionDeclaration] '!'
;

TagId:
	'$' tagName=ID '$'
;

ComponentDeclaration:
	'%' name=ID '%' (control=ComponentController)?
;

ComponentController:
	'{' 
		'view:' view=HtmlSnippet
		('get:' getter=JsSnippet
		'set:' setter=JsSnippet
		'validate:' validater=JsSnippet)?
	'}'
;

HtmlSnippet:
	{HtmlSnippet}
	html += CodeLiteral | (html +=CodeLiteralStart ( html+=HtmlAnnotation) (html+=CodeLiteralMid ( html+=HtmlAnnotation))* html+=CodeLiteralEnd) 
;

HtmlAnnotation:
	TagId|ChildId|ComponentId
;

CodeLiteral:
	value = CODENOTAG	
;
CodeLiteralStart:
	value = CODESTART	
;
CodeLiteralMid:
	value = CODEMID	
;
CodeLiteralEnd:
	value = CODEEND	
;

JsSnippet:
	{JsSnippet}
	js += CodeLiteral | (js +=CodeLiteralStart ( js+=JsExtraTag) (js+=CodeLiteralMid ( js+=JsExtraTag))* js+=CodeLiteralEnd) 
;

JsExtraTag:
	TagId
;
    
terminal CODENOTAG:
	'[' (('\\['|'\\]')|(("<"!("<"|'['|']')) | (">" !(">"|'['|']')))|!("<"|'['|']'|">"))* ("<"|">")?(']');
terminal CODESTART:
	'[' (('\\['|'\\]')|(("<"!("<"|'['|']')) | (">" !(">"|'['|']')))|!("<"|'['|']'|">"))* (">")?'<<';
terminal CODEMID:
	'>>' (('\\['|'\\]')|(("<"!("<"|'['|']')) | (">" !(">"|'['|']')))|!("<"|'['|']'|">"))* (">")?'<<';
terminal CODEEND:
	'>>' (('\\['|'\\]')|(("<"!("<"|'['|']')) | (">" !(">"|'['|']')))|!("<"|'['|']'|">"))* ("<"|">")?']';

terminal ID: 
	'^'?('a'..'z'|'A'..'Z'|'_')('a'..'z'|'A'..'Z'|'_'|'0'..'9')*;
     
terminal INT returns ecore::EInt: 
   	('0'..'9')+;
   	
terminal ML_COMMENT: 
	'/*' -> '*/';
     
terminal SL_COMMENT: 
    '//' !('\n'|'\r')* ('\r'? '\n')?;
     
terminal WS: 
	(' '|'\t'|'\r'|'\n')+;
     
terminal ANY_OTHER: 
   .;
    